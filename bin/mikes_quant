#!/usr/bin/env perl

use strict;
use warnings;
use 5.012;

use Getopt::Long;
use List::Util qw/sum/;
use List::MoreUtils qw/uniq/;

use constant FLOAT_TOL => 0.000001;

my $max_gaps  = 1;
my $min_len   = 3;
my $min_score = 0;
my $max_mass  = 8000;
my $min_mass  = 600;
my $tol       = 10;
my $by_charge = 0;
my $method    = 'trapezoid'; # 'trapezoid' or 'sum'
my $fn_pepxml;

GetOptions(
    'max_gaps=i'  => \$max_gaps,
    'min_len=i'   => \$min_len,
    'min_score=f' => \$min_score,
    'max_mass=f'  => \$max_mass,
    'min_mass=f'  => \$min_mass,
    'by_charge'   => \$by_charge,
    'method=s'    => \$method,
    'pepxml=s'    => \$fn_pepxml,
    'tol=f'       => \$tol,
);

my $scan_curr;
my $rt_curr;
my $index_curr = 0;

my @candidates;
my @finalized;

#parse peptide IDS if given

my @ids;

if (defined $fn_pepxml) {

    require MS::Reader::PepXML;

    my $p = MS::Reader::PepXML->new($fn_pepxml);

    my $n = $p->n_lists;
    for (0..$n-1) {
        $p->goto_list($_);
        while (my $result = $p->next_result) {
            my $hit = $result->get_hit(0);
            my $mass = $hit->{calc_neutral_pep_mass}
                // die "missing neutral mass";
            my $rt = $result->{retention_time_sec}
                // die "missing retention time";
            $rt /= 60; # report in minutes
            my $pep = $hit->{peptide}
                // die "missing peptide sequence";
            push @ids, {
                mass => $mass,
                rt   => $rt,
                pep  => $pep,
            };
            
        }
    }

}

# sort IDs by mass
@ids = sort {$a->{mass} <=> $b->{mass}} @ids;


LINE:
while (my $line = <STDIN>) {
    chomp $line;
    my ($type, @fields) = split "\t", $line;

    # spectrum header
    if ($type eq 'S') {

        my ($scan, $rt, $fn, $pre, @other) = @fields;
        die "ERROR: Only MS1 scans are supported\n"
            if ($pre > 0);
        $scan_curr = $scan;
        $rt_curr   = $rt;
        ++$index_curr;

        # finalize candidates where possible
        my $i = 0;
        while ($i < scalar(@candidates)) {

            my $cand = $candidates[$i];

            if ($index_curr - $cand->{last_index} - 1 <= $max_gaps) {
                ++$i;
                next;
            }

            splice @candidates, $i, 1;
            push @finalized, $cand
                if ($cand->{n_scans} >= $min_len);

        }

        @candidates = sort {$a->{mass} <=> $b->{mass}} @candidates;

    }

    elsif ($type eq 'P') {

        my ($mass, $z, $int, $base, $win, $na, $mod, $score) = @fields;
        next LINE if ($score < $min_score);
        next LINE if ($mass < $min_mass);
        next LINE if ($mass > $max_mass);

        my $merged = 0;

        my $lower = $mass - $mass*$tol/1000000;
        my $i = find_nearest($lower);
        my $last_c = $i;

        CANDIDATE:
        for my $c ($i..$#candidates) {

            $last_c = $c;

            my $cand = $candidates[$c];

            my $delta = $cand->{mass} - $mass;
            last CANDIDATE if ($delta >  $mass*$tol/1000000);
            next CANDIDATE if ($delta < -$mass*$tol/1000000);
            next CANDIDATE if ($by_charge && $cand->{z} != $z);

            next CANDIDATE
                if ($index_curr - $cand->{last_index} - 1 > $max_gaps);

            $cand->{n_scans}   += 1
                if ($index_curr > $cand->{last_index});
            $cand->{last_index} = $index_curr;
            $cand->{last_scan}  = $scan_curr;
            $cand->{last_rt}    = $rt_curr;
            $merged = 1;
            $cand->{ints}->{$z}->{$rt_curr} += $int;
            $cand->{int_sums}->{$rt_curr}   += $int;

            if ($int > $cand->{max_int}) {
                $cand->{max_int} = $int;
                $cand->{mass}    = $mass;
                $cand->{max_rt}  = $rt_curr;
                splice @candidates, $c, 1;
                my $t = find_nearest($mass);
                splice @candidates, $t, 0, $cand;
            }

            last CANDIDATE;

        }

        next LINE if $merged;

        splice @candidates, $last_c, 0, {
            mass        => $mass,
            first_index => $index_curr,
            last_index  => $index_curr,
            first_scan => $scan_curr,
            last_scan  => $scan_curr,
            first_rt    => $rt_curr,
            last_rt     => $rt_curr,
            n_scans     => 1,
            ints        => { $z => { $rt_curr => $int } },
            int_sums    => { $rt_curr => $int },
            max_int     => $int,
            max_rt      => $rt_curr,
            z           => $z,
        };


    }
    else {
        die "ERROR: Unexpected field type $type\n";
    }
}

# do final push
for my $cand (@candidates) {

    push @finalized, $cand
        if ($cand->{n_scans} >= $min_len);

}

@finalized = sort {$a->{mass} <=> $b->{mass}} @finalized;

# print header
say join "\t", qw/
    first_scan
    last_scan
    scan_range
    n_scans
    z_obs
    neutral_mass
    quant
    first_rt
    last_rt
    peak_rt
    n_psms
    peptide
    z_dist
    profiles
/;

my $id_start = 0;

FEATURE:
for my $feat (@finalized) {

    my %int_sums = %{ $feat->{int_sums} };
    my $sum_sums = sum values %int_sums;

    my @zs  = sort {$a <=> $b} keys %{ $feat->{ints}     };
    my @rts = sort {$a <=> $b} keys %{ $feat->{int_sums} };

    my $quant = $sum_sums;

    # use trapezoidal estimate of AUC
    if ($method eq 'trapezoid') {

        my $integrated = 0;
        if (@rts < 2) {
            $integrated = $feat->{int_sums}->{$rts[0]};
        }
        else {
            for (1..$#rts) {
                my $r1 = $rts[$_-1];
                my $r2 = $rts[$_];
                $integrated +=
                    ($feat->{int_sums}->{$r1}
                    + $feat->{int_sums}->{$r2}) / 2
                    *($r2 - $r1);
            }
        }
        $quant = $integrated;

    }

    my %fracs;
    for my $z (@zs) {
        my $total;
        for my $rt (@rts) {
            $total += $feat->{ints}->{$z}->{$rt}
                if (defined $feat->{ints}->{$z}->{$rt});
        }
        $fracs{$z} = sprintf '%0.3f', $total / $sum_sums;
    }

    my @int_strings;
    for my $z (@zs) {
        my @points = map {[ $_ => $feat->{ints}->{$z}->{$_} ]}
            grep {defined $feat->{ints}->{$z}->{$_}} @rts;
        @points = sort {$a->[0] <=> $b->[0]} @points;
        push @int_strings, "$z:" .
            join(',', map {$_->[0], $_->[1]} @points);
    }

    # check for peptide match
    my $mz_lower = $feat->{mass} - $feat->{mass}*$tol/1000000;
    my $mz_upper = $feat->{mass} + $feat->{mass}*$tol/1000000;
    my $rt_lower = $feat->{first_rt};
    my $rt_upper = $feat->{last_rt};

    my @pep_hits;

    ID:
    for my $i ($id_start..$#ids) {

        my $id = $ids[$i];
        if ($id->{mass} < $mz_lower) {
            $id_start = $i+1;
            next ID;
        }
        last ID if ($id->{mass} > $mz_upper);
        next ID if ($id->{rt} < $rt_lower - FLOAT_TOL
                 || $id->{rt} > $rt_upper + FLOAT_TOL);
        push @pep_hits, $id->{pep};
    } 

    my $n_psm = scalar @pep_hits;

    say join "\t",
        $feat->{first_scan},  
        $feat->{last_scan},  
        $feat->{last_index} - $feat->{first_index} + 1,
        $feat->{n_scans},  
        join(',', @zs),
        $feat->{mass},
        $quant,
        $feat->{first_rt},  
        $feat->{last_rt},
        $feat->{max_rt},
        $n_psm,
        join( '|', uniq @pep_hits),
        join( '|', map {"$_:$fracs{$_}"} keys %fracs ),
        join( '|', @int_strings );

}

sub find_nearest {

    my ($mass) = @_;

    return 0 if (scalar(@candidates) < 1);

    # binary search
    my ($lower, $upper) = (0, $#candidates);
    while ($lower != $upper) {
        my $mid = int( ($lower+$upper)/2 );
        ($lower,$upper) = $mass > $candidates[$mid]->{mass} + FLOAT_TOL
            ? ( $mid+1 , $upper )
            : ( $lower , $mid   );
    }

    if ($lower == $#candidates && $candidates[$lower]->{mass} < $mass) {
        return $lower + 1;
    }

    return $lower;

}
